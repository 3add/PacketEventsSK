plugins {
    id('java')
    id("com.gradleup.shadow") version("9.3.0")
    id("xyz.jpenilla.run-paper") version "2.3.1"
    id("io.papermc.paperweight.userdev") version "2.0.0-beta.19"
}

group = 'threeadd'
version = '1.0'

repositories {
    mavenCentral()

    maven { // paper (obv)
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }

    maven { // skript
        url = 'https://repo.skriptlang.org/releases'
    }

    maven {
        name = "jitpack"
        url = "https://jitpack.io"
    }

    maven { // packetevents
        name = "code-mc"
        url = uri("https://repo.codemc.io/repository/maven-releases/")
    }
}

dependencies {
    paperweight.paperDevBundle("1.21.10-R0.1-SNAPSHOT")

    compileOnly("com.github.retrooper:packetevents-spigot:2.10.1")
    compileOnly("com.github.SkriptLang:Skript:2.13.0") {
        transitive = false
    }
    compileOnly("com.github.ShaneBeee:SkBee:3.5.8")

    implementation(files('libs/EntityLib-spigot-3.0.3-SNAPSHOT.jar'))
}

build {
    dependsOn(shadowJar)
}

shadowJar {
    archiveClassifier.set("")
    relocate("me.tofaa.entitylib", "threeadd.entitylib")
    relocate("org.objenesis", "threeadd.objenesis")
}

tasks {
  runServer {
    minecraftVersion("1.21")
  }
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset = 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
